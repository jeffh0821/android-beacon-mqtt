apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.2'
    testBuildType "staging"

    defaultConfig {
        applicationId "org.turbo.beaconmqtt"
        minSdkVersion 19
        targetSdkVersion 30
        versionName "0.3"
        versionCode 321
        vectorDrawables.useSupportLibrary = true
        buildConfigField 'boolean', 'PRO', 'false'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'alias'
            keyPassword 'password'
            storeFile file('release.keystore')
            storePassword 'password'
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
            debuggable true
        }
        pro {
            applicationIdSuffix = ".pro"
            versionNameSuffix = " Pro"
            signingConfig signingConfigs.release
            buildConfigField 'boolean', 'PRO', 'true'
        }
        staging {
            applicationIdSuffix = ".staging"
            versionNameSuffix = " Staging"
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${applicationName}-${defaultConfig.versionName}-${defaultConfig.versionCode}.apk"
        }
    }

    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError true
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }
}

repositories {
    maven {
        url "https://repo.eclipse.org/content/repositories/paho-releases/"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'org.altbeacon:android-beacon-library:2.16.3'
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.2'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation('org.eclipse.paho:org.eclipse.paho.android.service:1.1.1') {
        exclude module: 'support-v4'
    }
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'net.danlew:android.joda:2.10.3'
    implementation 'com.rengwuxian.materialedittext:library:2.1.4'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
}
